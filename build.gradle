group 'ch.bildspur'
version '0.4.0'

apply plugin: 'java'

sourceCompatibility = 1.8

def javaCvVersion = '1.5.3-SNAPSHOT'
def outputName = 'deepvision'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

configurations {
    jar.archiveName = outputName + '.jar'

    processing
    compile.extendsFrom processing
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
}

dependencies {
    // compile
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // opencv (make sure you have updated the local maven repo: mvn -U compile)
    compile group: 'org.bytedeco', name: 'javacv', version: "$javaCvVersion"
    compile group: 'org.bytedeco', name: 'opencv-platform', version: "4.2.0-$javaCvVersion"
    compile group: 'org.bytedeco', name: 'openblas-platform', version: "0.3.9-$javaCvVersion"
    compile group: 'org.bytedeco', name: 'tesseract-platform', version: "4.1.1-$javaCvVersion"

    // processing
    processing group: 'org.processing', name: 'core', version: '3.3.7'
    processing group: 'org.jogamp.jogl', name: 'jogl-all-main', version: '2.3.2'
    processing group: 'org.jogamp.gluegen', name: 'gluegen-rt-main', version: '2.3.2'
}

task fatJar(type: Jar) {
    archiveName = "$outputName-complete.jar"
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.processing).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}